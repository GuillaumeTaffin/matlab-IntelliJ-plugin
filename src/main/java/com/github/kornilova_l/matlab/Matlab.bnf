{
  parserClass="com.github.kornilova_l.matlab.MatlabParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Matlab"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.kornilova_l.matlab.psi"
  psiImplPackage="com.github.kornilova_l.matlab.psi.impl"

  elementTypeHolderClass="com.github.kornilova_l.matlab.psi.MatlabTypes"
  elementTypeClass="com.github.kornilova_l.matlab.psi.MatlabElementType"
  tokenTypeClass="com.github.kornilova_l.matlab.psi.MatlabTokenType"

  extends(".*expr")=expr

  tokens=[
    dot="."
  ]
}

root ::= element *
private element ::= ( expr ( ";" | newline ) ) | comment | newline
private rule_recover ::= !( semicolon | newline )
private multiline_rule_recover ::= !( end )

expr ::= function_group
  | assign_expr
  | unary_group
  | conditional_group
  | logical_group
  | mul_group
  | add_group
  | element_by_group
  | primary_group
  | flow_group
  | class_group
  | load_group


private unary_group ::= unary_min_expr | unary_not_expr | transpose_expr { recoverWhile=rule_recover }
private primary_group ::= literal_expr | simple_ref_expr | paren_expr { recoverWhile=rule_recover }
private conditional_group ::= conditional_expr { recoverWhile=rule_recover }
private mul_group ::= mul_expr | div_expr | left_div_expr | power_expr { recoverWhile=rule_recover }
private add_group ::= plus_expr | minus_expr { recoverWhile=rule_recover }
private element_by_group ::= el_mul_expr | el_div_expr | el_left_div_expr | el_power_expr | el_complex_conjugate_transpose_expr { recoverWhile=rule_recover }
private function_group ::= function_expr | function_declaration_expr { recoverWhile=multiline_rule_recover }
private logical_group ::= matrix_and_expr | matrix_or_expr | and_expr | or_expr | vector_expr { recoverWhile=rule_recover }
private flow_group ::= if_expr | while_expr | for_expr { recoverWhile=multiline_rule_recover }
private class_group ::= class_def_expr { recoverWhile=multiline_rule_recover }
private load_group ::= load_expr

literal_expr ::= number | string | array { pin=1 }
simple_ref_expr ::= id { pin=1 }
unary_min_expr ::= minus expr { pin=1 }
unary_not_expr ::= not expr { pin=1 }
transpose_expr ::= expr transpose { pin=2 }
assign_expr ::= expr assign expr { pin=2 rightAssociative=true }
conditional_expr ::= expr ( less | more | lessorequal | moreorequal | equal | notequal ) expr { pin=2 }
load_expr ::= load filename

div_expr ::= expr delete expr { pin=2 }
left_div_expr ::= expr backslash expr { pin=2 }
mul_expr ::= expr mul expr { pin=2 }
power_expr ::= expr pow expr { pin=2 }

el_div_expr ::= expr dotdelete expr { pin=2 }
el_left_div_expr ::= expr dotbackslash expr { pin=2 }
el_mul_expr ::= expr dotmul expr { pin=2 }
el_power_expr ::= expr dotpow expr { pin=2 }
el_complex_conjugate_transpose_expr ::= expr dottranspose expr { pin=2 }

minus_expr ::= expr minus expr { pin=2 }
plus_expr ::= expr plus expr { pin=2 }

matrix_and_expr ::= expr matrixand expr { pin=2 }
matrix_or_expr ::= expr matrixor expr { pin=2 }
and_expr ::= expr and expr { pin=2 }
or_expr ::= expr or expr { pin=2 }
vector_expr ::= expr colon expr { pin=2 }

paren_expr ::= ob expr cb { pin=3 }

array ::= vector | array_with_numbers { pin=1 }
vector ::= ob expr [ colon expr ] [ colon expr ] cb { pin=3 }
array_with_numbers ::= opensquarebracket (semicolon* expr)* closesquarebracket { pin=3 }

number ::= ( float | floatexponential | integer ) { pin=1 }

function_expr ::= id ob arg_list? cb { pin=2 }
arg_list ::= expr (coma expr)* { pin=1 }

function_declaration_expr ::= function
        ( [ ( id | ( opensquarebracket arg_list? closesquarebracket ) ) ] assign )?
        id ob arg_list? cb element* end { pin=1 }

if_expr ::= if expr element*
        ( elseif expr element* )*
        [ else element* ]
        end { pin=1 }

while_expr ::= while expr element* end { pin=1 }

for_expr ::= for expr element* end { pin=1 }

class_def_expr ::= classdef id newline?
        properties newline? (id newline? )* end newline?
        methods newline? (function_declaration_expr newline?)* end newline?
        end newline? { pin=1 }